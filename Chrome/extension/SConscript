import os.path


if ARGUMENTS.get('DEPLOY') == 'ChromeExtension':
    Import('env', 'ZipFiles')
    ZipFiles('oplop.zip',
                'manifest.json', '*.html', 'icons/*.png', 'css/*.css',
                'biggie.js', 'content_script.js',
                'closure-library/closure/goog/css/autocomplete.css',
                'closure-library/closure/goog/css/dialog.css')
    RELEASE = True
else:
    RELEASE = False


Import('env', 'RenderTemplate')
Import('env', 'CLOSURE_COMPILER')


RenderTemplate('popup.html',
                ['templates/popup.html', '#/HTML5/index.html'],
                RELEASE=RELEASE)

RenderTemplate('options.html', ['templates/options.html'], RELEASE=RELEASE)


if not os.path.exists(str(CLOSURE_COMPILER)):
    print('Closure Compiler required to deploy the Chrome web app')
else:
    # Must list copied files specifically so they are depended upon.
    Command('biggie.js',
            ['externs/chrome_extensions.js',
                'js/oplop.js', 'js/md5.js', 'js/ui.js'] + Glob('js/*.js'),
        'python ./Chrome/extension/closure-library/closure/bin/build/closurebuilder.py '
        # Where to look for JS code.
        '--root Chrome/extension/closure-library --root Chrome/extension/js '
        # What namespaces to use to decide what to include.
        '--namespace="oplop.ui.extension" --namespace="oplop.options" '
        # Compile the JS code.
        '--output_mode compiled '
        # Where the compiler is.
        '--compiler_jar={compiler} '
        # Advanced compilation.
        '--compiler_flags="--compilation_level=ADVANCED_OPTIMIZATIONS" '
        # Externs file to silence errors about references to Chrome APIs.
        '--compiler_flags="--externs=Chrome/extension/externs/chrome_extensions.js" '
        # Print out all warnings for debugging purposes.
        '--compiler_flags="--warning_level=VERBOSE" > '
        '$TARGET'.format(compiler=str(CLOSURE_COMPILER)))


SConscript(['/'.join([name, 'SConscript'])
            for name in ('icons', 'css', 'js', 'externs')])
